#! /bin/csh -f

### Change these to your own site and user directory! 
### You will need to create a Makefile Macro in bld/ and a run_ice script 
### in input_templates/.

setenv DEBUG no
#setenv DEBUG yes

setenv SITE `hostname`
#setenv SITE vayu.nci.org.au
#setenv SITE petteri-as
#setenv NETCDF_ROOT /usr
#setenv SITE NAVO.kraken
#setenv SITE NCAR.bluevista
#setenv SITE LLNL.atlas

# load intel compilers if on vayu
# if ($SITE == 'vayu.nci.org.au') then
if ($SITE =~ vayu?) then
    setenv SITE 'vayu.nci.org.au'
    source /etc/csh.login
    module purge
    module load intel-fc intel-cc intel-mkl openmpi netcdf
    setenv SYSTEM_USERDIR /short/p66/${USER}/CICE4.1   # vayu
else    
    setenv SYSTEM_USERDIR $PWD/CICE4.1   # petteri-as
    setenv NETCDF_ROOT=/usr
endif

### SYSTEM_USERDIR is predefined on ORNL machines
#setenv SYSTEM_USERDIR /scr/eclare                # kraken
#setenv SYSTEM_USERDIR /ptmp/eclare               # bluevista
#setenv SYSTEM_USERDIR /p/lscratchb/hunke1        # atlas

### Grid resolution
#setenv RES gx3 ; setenv GRID 100x116
#setenv RES gx1 ; setenv GRID 320x384
#setenv RES tx1 ; setenv GRID 360x240
#setenv RES col ; setenv GRID 5x5
setenv RES auscom ; setenv GRID 360x300

set NXGLOB = `echo $GRID | sed s/x.\*//`
set NYGLOB = `echo $GRID | sed s/.\*x//`

# Recommendations:
#   NTASK equals nprocs in ice_in 
#   use processor_shape = slenderX1 or slenderX2 in ice_in
#   one per processor with distribution_type='cartesian' or
#   squarish blocks with distribution_type='rake'
# If BLCKX (BLCKY) does not divide NXGLOB (NYGLOB) evenly, padding 
# will be used on the right (top) of the grid.
if ($RES == 'col') then   # for column configuration:
   setenv NTASK      1       # total number of processors
   setenv BLCKX      5       # x-dimension of blocks ( not including )
   setenv BLCKY      5       # y-dimension of blocks (  ghost cells  )
else                      # test problem: gx3
   #setenv NTASK      6       # total number of processors
   #setenv BLCKX     60       # x-dimension of blocks ( not including )
   #setenv BLCKY     50       # y-dimension of blocks (  ghost cells  )
   setenv NTASK     16       # total number of processors
   setenv BLCKX     30       # x-dimension of blocks ( not including )
   setenv BLCKY     25       # y-dimension of blocks (  ghost cells  )
   setenv BLCKX     30       # x-dimension of blocks ( not including )
   setenv BLCKY     25       # y-dimension of blocks (  ghost cells  )
endif

if ($DEBUG == 'yes') then
   setenv NTASK     1         # total number of processors
   setenv BLCKX     360       # x-dimension of blocks ( not including )
   setenv BLCKY     300       # y-dimension of blocks (  ghost cells  )
endif

setenv BLCKX     `expr $NXGLOB / $NTASK`
setenv BLCKY     `expr $NYGLOB / $NTASK` 

# may need to increase MXBLCKS with rake distribution or padding
@ a = $NXGLOB * $NYGLOB ; @ b = $BLCKX * $BLCKY * $NTASK  
@ m = $a / $b ; setenv MXBLCKS $m ; if ($MXBLCKS == 0) setenv MXBLCKS 1
if ($NTASK == 16) then
   setenv MXBLCKS 26 # if necessary (code will print proper value)
endif

### Specialty code
setenv USE_ESMF no        # set to yes for ESMF runs
setenv CAM_ICE  no        # set to yes for CAM runs (single column) 
setenv SHRDIR   csm_share # location of CCSM shared code
setenv NETCDF   yes       # set to no if netcdf library is unavailable
setenv DITTO    no        # reproducible diagnostics
# ACCESS CICE settings yes/no
# ACCICE denotes standalone CICE with AusCOM
# modifications turned on
setenv ACCICE yes

### Set SRCDIR and EXEDIR to your own paths!
setenv SRCDIR $PWD
#setenv SRCDIR $HOME/cice                                  # kraken
#setenv SRCDIR /g/g21/hunke1/cice                          # atlas
#setenv SRCDIR /ptmp/eclare/cice                           # bluevista

setenv EXEDIR $SYSTEM_USERDIR/rundir4
                                          if !(-d $EXEDIR) mkdir -p $EXEDIR
setenv CBLD   $SRCDIR/bld
setenv OBJDIR $EXEDIR/compile           ; if !(-d $OBJDIR) mkdir -p $OBJDIR
setenv RSTDIR $EXEDIR/restart           ; if !(-d $RSTDIR) mkdir -p $RSTDIR
setenv HSTDIR $EXEDIR/history           ; if !(-d $HSTDIR) mkdir -p $HSTDIR

setenv ARCH `uname -s`
if ( $ARCH == 'UNICOS/mp') setenv ARCH UNICOS
if ( $ARCH == 'UNICOS') then
   cp -f $CBLD/Makefile.$ARCH $CBLD/Makefile
else
   cp -f $CBLD/Makefile.std $CBLD/Makefile
endif
setenv ARCH $ARCH.$SITE

cd $SRCDIR/source

cd $EXEDIR

if !($RES == 'col') then
if !(-e grid)    cp $SRCDIR/input_templates/$RES/global_$RES.grid grid
if !(-e kmt)     cp $SRCDIR/input_templates/$RES/global_$RES.kmt kmt
endif
if !(-e ice_in)  cp $SRCDIR/input_templates/$RES/ice_in .
if !(-e run_ice) cp $SRCDIR/input_templates/run_ice.$ARCH run_ice

cd $RSTDIR

cp $SRCDIR/input_templates/$RES/iced_$RES* .
if !(-e ice.restart_file) cp $SRCDIR/input_templates/$RES/ice.restart_file .

cd $OBJDIR

if ($NTASK == 1) then
   setenv COMMDIR serial
else
   setenv COMMDIR mpi 
endif

if ($USE_ESMF == 'yes') then
  setenv DRVDIR esmf
else if ($ACCICE == 'yes') then
  setenv DRVDIR accice
else  
  setenv DRVDIR cice4
endif

### AusCOM specific preprocessor flag which does not have an impact 
### in standalone runs
setenv N_ILYR 4

### List of source code directories (in order of importance).
cat >! Filepath << EOF
$SRCDIR/drivers/$DRVDIR
$SRCDIR/source
$SRCDIR/$COMMDIR
$SRCDIR/$SHRDIR
EOF

cc -o makdep $CBLD/makdep.c                         || exit 2

setenv MACFILE $CBLD/Macros.$ARCH

gmake VPFILE=Filepath EXEC=$EXEDIR/cice${NTASK}p \
      NXGLOB=$NXGLOB NYGLOB=$NYGLOB \
      BLCKX=$BLCKX BLCKY=$BLCKY MXBLCKS=$MXBLCKS \
  -f  $CBLD/Makefile MACFILE=$MACFILE || exit 2

cd ..
pwd                                         
echo NTASK = $NTASK
echo "global N, block_size" 
echo "x    $NXGLOB,    $BLCKX"
echo "y    $NYGLOB,    $BLCKY"
echo max_blocks = $MXBLCKS

